plugins {
    id "java"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

tasks.jar {
    archiveFileName.set("generator-minecraft-forge-1.20.1-2025.2-1.1.0.zip")
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))
    environment("MCREATOR_PLUGINS_DEV", "")

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath

    main = 'net.mcreator.Launcher'

    workingDir = mcreator_path
}

subprojects { subproject ->
    subproject.plugins.withType(JavaPlugin) {
        test {
            useJUnitPlatform()

            dependsOn rootProject.jar

            environment("MCREATOR_PLUGINS_FOLDER", new File(rootProject.projectDir, "/build/libs"))
            environment("MCREATOR_PLUGINS_DEV", "")
            environment("MCREATOR_TEST_GENERATORS", "forge-1.20.1, datapack-1.20.1")

            workingDir = mcreator_path
        }
    }
}